plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.spotbugs' version '1.7.1'
    id 'com.github.ksoichiro.console.reporter' version '0.6.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    def jmcVersion = '7.1.0-SNAPSHOT'
    api "org.openjdk.jmc:org.openjdk.jmc.flightrecorder.configuration:${jmcVersion}", {
        exclude group: 'org.jacoco', module: 'jacoco-maven-plugin'
    }
    api "org.openjdk.jmc:org.openjdk.jmc.rjmx:${jmcVersion}", {
        exclude group: 'org.jacoco', module: 'jacoco-maven-plugin'
    }
    api "org.openjdk.jmc:org.openjdk.jmc.rjmx.services.jfr:${jmcVersion}", {
        exclude group: 'org.jacoco', module: 'jacoco-maven-plugin'
    }
    api "org.openjdk.jmc:org.openjdk.jmc.jdp:${jmcVersion}", {
        exclude group: 'org.jacoco', module: 'jacoco-maven-plugin'
    }
    api "org.openjdk.jmc:org.openjdk.jmc.ui.common:${jmcVersion}", {
        exclude group: 'org.jacoco', module: 'jacoco-maven-plugin'
    }

    api 'org.eclipse.platform:org.eclipse.core.runtime:3.15.0'

    implementation 'org.apache.commons:commons-lang3:3.8.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    testImplementation 'org.hamcrest:hamcrest:2.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.25.1'
    testImplementation 'org.mockito:mockito-inline:2.25.1'
}

publishing {
    def coreVersion = '0.3.2';
    publications {
        maven(MavenPublication) {
            groupId = 'com.redhat.rhjmc'
            artifactId = 'containerjfr-core'
            version = coreVersion

            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

spotbugs {
  toolVersion = '4.0.0-beta1'
  sourceSets = [sourceSets.main]
  spotbugsTest.enabled = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
        html.destination file("${buildDir}/spotbugs.html")
    }
}

jacoco {
    toolVersion = '0.8.3'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test.finalizedBy jacocoTestReport

build.finalizedBy publishToMavenLocal

